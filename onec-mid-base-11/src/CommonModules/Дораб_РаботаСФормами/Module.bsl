
#Область ПрограммныйИнтерфейс

// Начало.БоклачПИ 22.12.2025 homework-13-7
Процедура ДополнитьФорму(Форма) Экспорт
	
	ИмяФормы = Форма.ИмяФормы;
	
	Если ИмяФормы = "Документ.ЗаказПокупателя.Форма.ФормаДокумента" Тогда
		ДобавитьПолеКонтактноеЛицоВГруппаШапкаПраво(Форма);
		ДобавитьПоляСогласованнаяСкидкаИПересчитатьТаблицуВГруппаШапкаЛево(Форма);
		ДобавитьПолеСтатусЗаказаВГруппаШапкаЛево(Форма);
	ИначеЕсли ИмяФормы = "Документ.ПоступлениеТоваровУслуг.Форма.ФормаДокумента" Тогда
		ДобавитьПолеКонтактноеЛицоВГруппаШапкаПраво(Форма);
	ИначеЕсли ИмяФормы = "Документ.РеализацияТоваровУслуг.Форма.ФормаДокумента" Тогда
		ДобавитьПолеКонтактноеЛицоВГруппаШапкаПраво(Форма);
	ИначеЕсли ИмяФормы = "Документ.ОплатаОтПокупателя.Форма.ФормаДокумента" Тогда
		ВставитьПолеКонтактноеЛицоНаФормуПередСуммаДокумента(Форма);
	ИначеЕсли ИмяФормы = "Документ.ОплатаПоставщику.Форма.ФормаДокумента" Тогда
		ВставитьПолеКонтактноеЛицоНаФормуПередСуммаДокумента(Форма);
	ИначеЕсли ИмяФормы = "Документ.ЗаказПокупателя.Форма.ФормаСписка" Тогда
		ВставитьКолонкуСтатусЗаказаНаФормуСписка(Форма);
	КонецЕсли;
	
КонецПроцедуры // Конец.Боклач 22.12.2025

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

// Начало.БоклачПИ 22.12.2025 homework-13-7
Процедура ДобавитьПолеКонтактноеЛицоВГруппаШапкаПраво(Форма) Экспорт
	
	ПолеВвода = Форма.Элементы.Добавить("Дораб_КонтактноеЛицо", Тип("ПолеФормы"), Форма.Элементы.ГруппаШапкаПраво);
	ПолеВвода.Вид = ВидПоляФормы.ПолеВвода;
	ПолеВвода.ПутьКДанным = "Объект.Дораб_КонтактноеЛицо";
	
КонецПроцедуры // Конец.Боклач 22.12.2025

// Начало.БоклачПИ 22.12.2025 homework-13-7
Процедура ВставитьПолеКонтактноеЛицоНаФормуПередСуммаДокумента(Форма) Экспорт
	
	ПолеВвода = Форма.Элементы.Вставить("Дораб_КонтактноеЛицо", Тип("ПолеФормы"), , Форма.Элементы.СуммаДокумента);
	ПолеВвода.Вид = ВидПоляФормы.ПолеВвода;
	ПолеВвода.ПутьКДанным = "Объект.Дораб_КонтактноеЛицо";
	
КонецПроцедуры // Конец.Боклач 22.12.2025

// Начало.БоклачПИ 22.12.2025 homework-13-7
Процедура ДобавитьПоляСогласованнаяСкидкаИПересчитатьТаблицуВГруппаШапкаЛево(Форма) Экспорт
	
	// Добавим на Форму обычную группу без отображения ГруппаСкидкаИКнопка
	ГруппаСкидкаИКнопка = Форма.Элементы.Добавить("ГруппаСкидкаИКнопка", Тип("ГруппаФормы"), Форма.Элементы.ГруппаШапкаЛево);
	ГруппаСкидкаИКнопка.Вид = ВидГруппыФормы.ОбычнаяГруппа;
	ГруппаСкидкаИКнопка.Отображение = ОтображениеОбычнойГруппы.Нет;
	ГруппаСкидкаИКнопка.ОтображатьЗаголовок = ЛОЖЬ; 
	ГруппаСкидкаИКнопка.Группировка = ГруппировкаПодчиненныхЭлементовФормы.ГоризонтальнаяВсегда;
	
	// Добавим на Форму реквизит СогласованнаяСкидка
	ПолеВвода = Форма.Элементы.Добавить("Дораб_СогласованнаяСкидка", Тип("ПолеФормы"), Форма.Элементы.ГруппаСкидкаИКнопка);
	ПолеВвода.Вид = ВидПоляФормы.ПолеВвода;
	ПолеВвода.ПутьКДанным = "Объект.Дораб_СогласованнаяСкидка";
	ПолеВвода.УстановитьДействие("ПриИзменении", "Дораб_СогласованнаяСкидкаПриИзменении");
	
	// Создадим команду ПересчитатьТаблицу
	НоваяКоманда = Форма.Команды.Добавить("ПересчитатьТаблицу");
	НоваяКоманда.Заголовок = "Пересчитать таблицу";
	НоваяКоманда.Действие = "Дораб_ПересчитатьТаблицу";
	
	// Создадим кнопку ПересчитатьТаблицу
	КнопкаПересчитатьТаблицу = Форма.Элементы.Добавить("ПересчитатьТаблицу", Тип("КнопкаФормы"), Форма.Элементы.ГруппаСкидкаИКнопка);
	КнопкаПересчитатьТаблицу.ИмяКоманды = "ПересчитатьТаблицу";
	КнопкаПересчитатьТаблицу.Отображение = ОтображениеКнопки.КартинкаИТекст;
	КнопкаПересчитатьТаблицу.Картинка = БиблиотекаКартинок.Обновить;
	
КонецПроцедуры // Конец.Боклач 22.12.2025

// Начало.БоклачПИ 22.12.2025 homework-13-7
Процедура ДобавитьПолеСтатусЗаказаВГруппаШапкаЛево(Форма) Экспорт
	
	ПолеВвода = Форма.Элементы.Добавить("Дораб_СтатусЗаказа", Тип("ПолеФормы"), Форма.Элементы.ГруппаШапкаЛево);
	ПолеВвода.Вид = ВидПоляФормы.ПолеВвода;
	ПолеВвода.ПутьКДанным = "Объект.Дораб_СтатусЗаказа";
	
КонецПроцедуры // Конец.Боклач 22.12.2025

// Начало.БоклачПИ 22.12.2025 homework-13-7
Процедура ВставитьКолонкуСтатусЗаказаНаФормуСписка(Форма) Экспорт
	
	НоваяКолонкаСтатусЗаказа = Форма.Элементы.Вставить("Дораб_СтатусЗаказа", Тип("ПолеФормы"),Форма.Элементы.Список, Форма.Элементы["ЕстьФайлы"]);
	НоваяКолонкаСтатусЗаказа.Вид = ВидПоляФормы.ПолеНадписи;
	НоваяКолонкаСтатусЗаказа.Заголовок = "Статус заказа";
	
	СхемаЗапроса = Новый СхемаЗапроса;
	СхемаЗапроса.УстановитьТекстЗапроса(Форма["Список"].ТекстЗапроса);
	
	Оператор = СхемаЗапроса.ПакетЗапросов[0].Операторы[0];
	
	ВыбираемыеПоля = Оператор.ВыбираемыеПоля;
	
	ВыбираемыеПоля.Добавить(" ДокументЗаказПокупателя.Дораб_СтатусЗаказа");
	
	Форма["Список"].ТекстЗапроса = СхемаЗапроса.ПолучитьТекстЗапроса();
	
	НоваяКолонкаСтатусЗаказа.ПутьКДанным = "Список.Дораб_СтатусЗаказа";
	
КонецПроцедуры // Конец.Боклач 22.12.2025

#КонецОбласти
