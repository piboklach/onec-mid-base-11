
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Начало.БоклачПИ 22.12.2025 homework-13-7
Процедура ПриОпределенииНастроекПечати(НастройкиОбъекта) Экспорт
	НастройкиОбъекта.ПриДобавленииКомандПечати = Истина;
КонецПроцедуры // Конец.Боклач 22.12.2025

// Начало.БоклачПИ 22.12.2025 homework-13-7
Процедура ДобавитьКомандыПечати(КомандыПечати) Экспорт
	
	// Анкета клиента
	КомандаПечати = КомандыПечати.Добавить();
	КомандаПечати.Идентификатор = "АнкетаКлиента";
	КомандаПечати.Представление = НСтр("ru = 'Анкета клиента'");
	КомандаПечати.Порядок = 5;
	
	// Товарная накладная
	КомандаПечати = КомандыПечати.Добавить();
	КомандаПечати.Идентификатор = "ТоварнаяНакладная";
	КомандаПечати.Представление = НСтр("ru = 'Товарная накладная'");
	КомандаПечати.Порядок = 10;
	
	// Комплект документов
	КомандаПечати = КомандыПечати.Добавить();
	КомандаПечати.Идентификатор = "АнкетаКлиента,ТоварнаяНакладная";
	КомандаПечати.Представление = НСтр("ru = 'Комплект документов'");
	КомандаПечати.Порядок = 100;
	
	// Договор на доставку Word (Docx)
	КомандаПечати = КомандыПечати.Добавить();
	КомандаПечати.МенеджерПечати = "УправлениеПечатью";
	КомандаПечати.Идентификатор = "Документ.Дораб_Доставка.ПФ_DOC_ДоговорНаДоставку";
	КомандаПечати.Представление = НСтр("ru = 'Договор на доставку (Docx)'");
	КомандаПечати.Порядок = 150;
	
КонецПроцедуры // Конец.Боклач 22.12.2025

// Начало.БоклачПИ 22.12.2025 homework-13-7
Процедура Печать(МассивОбъектов, ПараметрыПечати, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода) Экспорт
	
	ПечатнаяФорма = УправлениеПечатью.СведенияОПечатнойФорме(КоллекцияПечатныхФорм, "АнкетаКлиента");
	Если ПечатнаяФорма <> Неопределено Тогда
		ПечатнаяФорма.ТабличныйДокумент = ПечатьАнкетаКлиента(МассивОбъектов, ОбъектыПечати);
		ПечатнаяФорма.СинонимМакета = НСтр("ru = 'Анкета клиента'");
		ПечатнаяФорма.ПолныйПутьКМакету = "Документ.Дораб_Доставка.ПФ_MXL_АнкетаКлиента";
	КонецЕсли;
	
	ПечатнаяФорма = УправлениеПечатью.СведенияОПечатнойФорме(КоллекцияПечатныхФорм, "ТоварнаяНакладная");
	Если ПечатнаяФорма <> Неопределено Тогда
		ПечатнаяФорма.ТабличныйДокумент = ПечатьТоварнаяНакладная(МассивОбъектов, ОбъектыПечати);
		ПечатнаяФорма.СинонимМакета = НСтр("ru = 'Товарная накладная'");
		ПечатнаяФорма.ПолныйПутьКМакету = "Документ.Дораб_Доставка.ПФ_MXL_ТоварнаяНакладная";
	КонецЕсли;
	
КонецПроцедуры // Конец.Боклач 22.12.2025

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Начало.БоклачПИ 22.12.2025 homework-13-7
Функция ПечатьАнкетаКлиента(МассивОбъектов, ОбъектыПечати)
	
	ТабличныйДокумент = Новый ТабличныйДокумент;
	ТабличныйДокумент.КлючПараметровПечати = "ПараметрыПечати_АнкетаКлиента";
	
	Макет = УправлениеПечатью.МакетПечатнойФормы("Документ.Дораб_Доставка.ПФ_MXL_АнкетаКлиента");
	
	ДанныеДокументов = ПолучитьДанныеДокументов(МассивОбъектов);
	
	ПервыйДокумент = Истина;
	
	Пока ДанныеДокументов.Следующий() Цикл
		
		Если Не ПервыйДокумент Тогда
			// Все документы нужно выводить на разных страницах.
			ТабличныйДокумент.ВывестиГоризонтальныйРазделительСтраниц();
		КонецЕсли;
		
		ПервыйДокумент = Ложь;
		
		НомерСтрокиНачало = ТабличныйДокумент.ВысотаТаблицы + 1;
		
		ВывестиАнкетаКлиента(ДанныеДокументов, ТабличныйДокумент, Макет);
		
		УправлениеПечатью.ЗадатьОбластьПечатиДокумента(ТабличныйДокумент, 
		НомерСтрокиНачало, ОбъектыПечати, ДанныеДокументов.Ссылка);
		
	КонецЦикла;
		
	Возврат ТабличныйДокумент;
	
КонецФункции // Конец.Боклач 22.12.2025

// Начало.БоклачПИ 22.12.2025 homework-13-7
Функция ПечатьТоварнаяНакладная(МассивОбъектов, ОбъектыПечати)
	
	ТабличныйДокумент = Новый ТабличныйДокумент;
	ТабличныйДокумент.КлючПараметровПечати = "ПараметрыПечати_ТоварнаяНакладная";
	
	Макет = УправлениеПечатью.МакетПечатнойФормы("Документ.Дораб_Доставка.ПФ_MXL_ТоварнаяНакладная");
	
	ДанныеДокументов = ПолучитьДанныеДокументов(МассивОбъектов);
	
	ПервыйДокумент = Истина;
	
	Пока ДанныеДокументов.Следующий() Цикл
		
		Если Не ПервыйДокумент Тогда
			// Все документы нужно выводить на разных страницах.
			ТабличныйДокумент.ВывестиГоризонтальныйРазделительСтраниц();
		КонецЕсли;
		
		ПервыйДокумент = Ложь;
		
		НомерСтрокиНачало = ТабличныйДокумент.ВысотаТаблицы + 1;
		
		ВывестиЗаголовокТоварнаяНакладная(ДанныеДокументов, ТабличныйДокумент, Макет);
		
		ВывестиТоварыТоварнаяНакладная(ДанныеДокументов, ТабличныйДокумент, Макет);
		
		ВывестиПодвалТоварнаяНакладная(ДанныеДокументов, ТабличныйДокумент, Макет);
		
		УправлениеПечатью.ЗадатьОбластьПечатиДокумента(ТабличныйДокумент, 
		НомерСтрокиНачало, ОбъектыПечати, ДанныеДокументов.Ссылка);
		
	КонецЦикла;
		
	Возврат ТабличныйДокумент;
	
КонецФункции // Конец.Боклач 22.12.2025

// Начало.БоклачПИ 22.12.2025 homework-13-7
Функция ПолучитьДанныеДокументов(МассивОбъектов)
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	Дораб_Доставка.Ссылка КАК Ссылка,
	               |	Дораб_Доставка.Номер КАК Номер,
	               |	Дораб_Доставка.Дата КАК Дата,
	               |	Дораб_Доставка.Контрагент КАК Контрагент,
	               |	Дораб_Доставка.Организация КАК Организация,
	               |	Дораб_Доставка.Товары.(
	               |		Ссылка КАК Ссылка,
	               |		НомерСтроки КАК НомерСтроки,
	               |		Номенклатура КАК Номенклатура,
	               |		Количество КАК Количество
	               |	) КАК Товары
	               |ИЗ
	               |	Документ.Дораб_Доставка КАК Дораб_Доставка
	               |ГДЕ
	               |	Дораб_Доставка.Ссылка В(&МассивОбъектов)";
	
	Запрос.УстановитьПараметр("МассивОбъектов", МассивОбъектов);
	
	Возврат Запрос.Выполнить().Выбрать();
	
КонецФункции // Конец.Боклач 22.12.2025

// Начало.БоклачПИ 22.12.2025 homework-13-7
Процедура ВывестиАнкетаКлиента(ДанныеДокументов, ТабличныйДокумент, Макет)
	
	ОбластьШаблонАнкеты = Макет.ПолучитьОбласть("ШаблонАнкеты");
	
	ДанныеПечати = Новый Структура;
	ДанныеПечати.Вставить("Номер", ПрефиксацияОбъектовКлиентСервер.НомерНаПечать(ДанныеДокументов.Номер));
	ДанныеПечати.Вставить("Дата", Формат(ДанныеДокументов.Дата, "ДФ=dd.MM.yyyy"));
	
	ОбластьШаблонАнкеты.Параметры.Заполнить(ДанныеПечати);
	
	ВывестиQRКод(ДанныеДокументов, ОбластьШаблонАнкеты);
	
	ТабличныйДокумент.Вывести(ОбластьШаблонАнкеты);
	
КонецПроцедуры // Конец.Боклач 22.12.2025

// Начало.БоклачПИ 22.12.2025 homework-13-7
Процедура ВывестиЗаголовокТоварнаяНакладная(ДанныеДокументов, ТабличныйДокумент, Макет)
	
	ОбластьШаблонТоварнаяНакладная = Макет.ПолучитьОбласть("Заголовок");
	
	ДанныеПечати = Новый Структура;
	ДанныеПечати.Вставить("Номер", ПрефиксацияОбъектовКлиентСервер.НомерНаПечать(ДанныеДокументов.Номер));
	ДанныеПечати.Вставить("Дата", Формат(ДанныеДокументов.Дата, "ДФ=dd.MM.yyyy"));
	ДанныеПечати.Вставить("Организация", ДанныеДокументов.Организация);
	ДанныеПечати.Вставить("Контрагент", ДанныеДокументов.Контрагент);
	
	ОбластьШаблонТоварнаяНакладная.Параметры.Заполнить(ДанныеПечати);
	
	ВывестиQRКод(ДанныеДокументов, ОбластьШаблонТоварнаяНакладная);
	
	ТабличныйДокумент.Вывести(ОбластьШаблонТоварнаяНакладная);
	
КонецПроцедуры // Конец.Боклач 22.12.2025

// Начало.БоклачПИ 22.12.2025 homework-13-7
Процедура ВывестиQRКод(ДанныеДокументов, Область)
	
	СтрокаСсылки = ПолучитьНавигационнуюСсылку(ДанныеДокументов.Ссылка);
	ДанныеQRКода = ГенерацияШтрихкода.ДанныеQRКода(СтрокаСсылки, 1, 120);
	
	Если НЕ ТипЗнч(ДанныеQRКода) = Тип("ДвоичныеДанные") Тогда
		ТекстСообщения = НСтр("ru = 'Не удалось сформировать QR-код.
		|Технические подробности см. в журнале регистрации.'");
		ОбщегоНазначения.СообщитьПользователю(ТекстСообщения);
	Иначе
		КартинкаQRКода = Новый Картинка(ДанныеQRКода);
		Область.Рисунки.QRКод.Картинка = КартинкаQRКода;
	КонецЕсли;
	
КонецПроцедуры // Конец.Боклач 22.12.2025

// Начало.БоклачПИ 22.12.2025 homework-13-7
Процедура ВывестиТоварыТоварнаяНакладная(ДанныеДокументов, ТабличныйДокумент, Макет)
	
	ОбластьШапкаТаблицы = Макет.ПолучитьОбласть("ТоварыШапка");
	ОбластьТоварыСтрока = Макет.ПолучитьОбласть("ТоварыСтрока");
	
	ТабличныйДокумент.Вывести(ОбластьШапкаТаблицы);
	
	ВыборкаТовары = ДанныеДокументов.Товары.Выбрать();
	Пока ВыборкаТовары.Следующий() Цикл
		ОбластьТоварыСтрока.Параметры.Заполнить(ВыборкаТовары);
		ТабличныйДокумент.Вывести(ОбластьТоварыСтрока);
	КонецЦикла;
	
КонецПроцедуры // Конец.Боклач 22.12.2025

// Начало.БоклачПИ 22.12.2025 homework-13-7
Процедура ВывестиПодвалТоварнаяНакладная(ДанныеДокументов, ТабличныйДокумент, Макет)
	
	ОбластьПодвал = Макет.ПолучитьОбласть("Подвал");
	
	ТабличныйДокумент.Вывести(ОбластьПодвал);
	
КонецПроцедуры // Конец.Боклач 22.12.2025

#КонецОбласти

#КонецЕсли